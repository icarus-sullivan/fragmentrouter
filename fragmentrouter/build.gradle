apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

group = 'com.github.icarus-sullivan'
version = '1.0.6'

ext {
    CapitalName = 'FragmentRouter'
    Name = 'fragmentrouter'
    SourceUrl = 'https://github.com/icarus-sullivan/fragmentrouter.git'
    Website = 'https://github.com/icarus-sullivan/fragmentrouter'
    Description = 'FragmentRouter is a simpler way to handle Fragment Navigation in Android projects. It operates like a Undo-Redo implementation making use of default position fragments to handle the navigation, but still relies on FragmentManager under the hood.'
    Version = '1.0.6'
    ArtifactId = 'fragmentrouter'
    Labels = ['aar', 'android', 'fragment', 'management']
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0.6"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
}


install {
    repositories.mavenInstaller {
        pom.project {
            name project.ext.CapitalName
            description project.ext.Description
            url project.ext.Website

            packaging 'aar'
            groupId 'com.github.icarus-sullivan'
            artifactId project.ext.ArtifactId
            version project.ext.Version

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection project.ext.SourceUrl
                url project.ext.Website

            }
            developers {
                developer {
                    name 'Chris Sullivan'
                    id 'icarus-sullivan'
                    email 'chrissullivan.dev@gmail.com'
                }
            }
        }
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = project.ext.Name
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = project.ext.SourceUrl
        version {
            name = project.ext.Version
            desc = project.ext.Description
            released  = new Date()
            vcsTag = project.ext.Version
        }
        labels = project.ext.Labels
        publicDownloadNumbers = true
    }
}